apply plugin: "com.github.ben-manes.versions"
apply plugin: "com.github.plnice.canidropjetifier"
//Check versions of dependencies: ./gradlew dependencyUpdates -Drevision=milestone -DoutputFormatter=json
//Force-update dependencies:      ./gradlew clean build --refresh-dependencies
//Check if I can drop jetifier:   ./gradlew -Pandroid.enableJetifier=false canIDropJetifier

buildscript {
    ext.kotlinGradle = '1.4.21'
    ext.toolsGradle = '4.2.0-beta03'

    repositories {
        google()
        jcenter()
        gradlePluginPortal()
    }


    dependencies {
        classpath "com.android.tools.build:gradle:$toolsGradle"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinGradle"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.3.2"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.36.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.21"
        classpath "com.github.plnice:canidropjetifier:0.5"
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->

            def isNonStable = { String version ->
                ['alpha', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                    version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
                }
            }

            if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
                selection.reject('Release candidate')
            }
        }
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    minSdk = 21
    targetSdk = 30
    compileSdk = 30
    buildTools = '30.0.2'

    //Core stuff
    //-------------------------------
    compat = '1.2.0'
    multidex = '2.0.1'
    androidX = '1.0.0'
    lifecycle = '2.2.0'
    coroutines = '1.4.2'
    navigation = '2.3.2'
    annotations = '1.1.0'
    recyclerView = '1.1.0'
    coordinatorLayout = '1.1.0'
    materialComps = '1.2.1'
    kotlin = ext.kotlinGradle
    ktx = '1.3.2'
    lifecycleLivedata = '2.2.0'

    //Do things online
    //-------------------------------
    moshi = '1.11.0'
    okHttp = '4.9.0'
    retrofit = '2.9.0'

    //Extras
    //-------------------------------
    timber = '4.7.1'
    coil = '1.1.1'
}

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs'
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 1

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.0"

    defaultConfig {
        applicationId "com.tinktest.sotiris"
        minSdkVersion 21
        targetSdkVersion 33

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        setProperty("archivesBaseName", applicationId + "-" + versionName + "." + versionBuild)

        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildFeatures.viewBinding = true
    dexOptions.javaMaxHeapSize "4G"
    lintOptions.abortOnError false
    aaptOptions.cruncherEnabled false

    packagingOptions {
        pickFirst("META-INF/atomicfu.kotlin_module")
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'LICENSE.txt'
    }

    buildTypes {
        debug {
            buildConfigField "String", "BASE_URL_FOR_PUGME", '"https://pugme.herokuapp.com/"'
            buildConfigField "String", "BASE_URL_FOR_DOGCEO", '"https://dog.ceo/"'
            buildConfigField "String", "BASE_URL_FOR_DOGFACTS", '"https://dog-api.kinduff.com/"'

            debuggable true

            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false

            jniDebuggable false
            renderscriptDebuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Core Android stuff
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.appcompat:appcompat:1.4.2"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.gridlayout:gridlayout:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "com.google.android.material:material:1.6.1"
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.5.1"
    kapt "androidx.annotation:annotation:1.4.0"

    //Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.5.1"

    //Coroutines
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"

    //Kotlin & friends
    implementation "androidx.core:core-ktx:1.8.0"
    //implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.7.10"

    //Retrofit + OkHttp
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
    implementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.6"
    implementation "com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.9"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:5.0.0-alpha.6"

    //Moshi
    implementation "com.squareup.moshi:moshi:1.13.0"
    implementation "com.squareup.moshi:moshi-kotlin:1.13.0"

    //Tools to make life easier
    implementation "io.coil-kt:coil:2.1.0"
    implementation "com.jakewharton.timber:timber:5.0.1"
}
